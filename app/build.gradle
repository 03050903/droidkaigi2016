apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'android-apt'

// Manifest version
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

// CI
def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

def packageName = "com.konifar.confsched"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    dataBinding {
        enabled = true
    }

    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }

    defaultConfig {
        applicationId packageName
        minSdkVersion 16
        targetSdkVersion 23
        versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            shrinkResources true
            debuggable true
            zipAlignEnabled true

            buildConfigField("String", "GA_TRACKING_ID", "\"${ga_tracking_id_debug}\"")
        }
        release {
            signingConfig signingConfigs.debug
            shrinkResources true
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField("String", "GA_TRACKING_ID", "\"${ga_tracking_id_release}\"")
        }
    }

    productFlavors {
        develop {
            applicationId "${packageName}.develop"
        }
        production {
            applicationId packageName
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    aaptOptions {
        noCompress 'apk'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile "com.android.support:support-v4:${support_lib_version}"
    compile "com.android.support:appcompat-v7:${support_lib_version}"
    compile "com.android.support:support-annotations:${support_lib_version}"
    compile "com.android.support:design:${support_lib_version}"
    compile "com.android.support:cardview-v7:${support_lib_version}"
    compile "com.android.support:recyclerview-v7:${support_lib_version}"

    compile "com.google.android.gms:play-services-analytics:${play_services_lib_version}"

    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.google.code.gson:gson:2.4'
    compile "com.squareup.okhttp3:okhttp:3.0.1"
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile "com.jakewharton.threetenabp:threetenabp:1.0.2"
    compile "org.parceler:parceler-api:1.0.4"
    apt "org.parceler:parceler:1.0.4"

    apt 'com.github.gfx.android.orma:orma-processor:1.0.1'
    compile 'com.github.gfx.android.orma:orma:1.0.1'

    compile "com.squareup.picasso:picasso:2.5.2"
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta3'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta3'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta3'

    compile 'org.apmem.tools:layouts:1.10@aar'
    compile 'com.github.jd-alexander:LikeButton:0.1.7'
    compile 'net.opacapp:multiline-collapsingtoolbar:1.0.0'

    debugCompile 'com.facebook.stetho:stetho:1.2.0'

    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test:runner:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.squareup.assertj:assertj-android:1.0.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
